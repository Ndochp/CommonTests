// Производит запуск клиента тестирования
// Параметры:
// Структура, Тестер.Запустить.Параметры

запустить1С ( _ );
подключиться ( _ );
подавитьБезопастность ( _ );

Процедура запустить1С ( Параметры )

	команда = """C:\Program Files (x86)\1cv8\common\1cestart.exe"" enterprise";
	п = Параметры.База;
	если ( п <> неопределено ) тогда
		команда = команда + " /IBName """ + п + """";
	конецесли;
	п = Параметры.Пользователь;
	если ( п <> неопределено ) тогда
		команда = команда + " /N """ + п + """";
	конецесли;
	п = Параметры.Пароль;
	если ( п <> неопределено ) тогда
		команда = команда + " /P """ + п + """";
	конецесли;
	п = Параметры.Порт;
	если ( п = неопределено ) тогда
		команда = команда + " /TESTCLIENT ";
	иначе
		команда = команда + " /TESTCLIENT -TPORT""" + Format ( п, "NG=0" ) + """";
	конецесли;
	п = Параметры.Обработка;
	если ( п = неопределено ) тогда
		открытьОбработку = ложь;
	иначе
		команда = команда + " /Execute """ + п + """";
		открытьОбработку = истина;
	конецесли;
	п = Параметры.Параметры;
	если ( п <> неопределено ) тогда
		команда = команда + " " + п;
	конецесли;
	ЗапуститьПриложение ( команда );
	
КонецПроцедуры

Процедура подключиться ( Параметры )

	ждать = Параметры.Ждать;
	если ( ждать = неопределено ) тогда
		возврат;
	конецесли;
	начало = ТекущаяДата ();
	пока ( истина ) цикл
		попытка
			Подключить ( , Параметры.Порт );
			прервать;
		исключение
		конецпопытки;
		прошло = ТекущаяДата () - начало;
		если ( прошло > ждать ) тогда
			прервать;
		конецесли;
	конеццикла;

КонецПроцедуры

Процедура подавитьБезопастность ( Параметры )

	если ( Параметры.Обработка = неопределено ) тогда
		возврат;
	конецесли;
	если ( не Дождаться ( "1?:*" ) ) тогда
		возврат;
	конецесли;
	форма = Здесь ( "1?:*" );
	поле = Тип ( "ТестируемоеПолеФормы" );
	надпись = форма.НайтиОбъект ( поле, "Предупреждение безопасности*" );
	если ( надпись = неопределено ) тогда
		надпись = форма.НайтиОбъект ( поле, "Security warning*" );
		да = "Yes";
		если ( надпись = неопределено ) тогда
			возврат;
		конецесли;
	иначе
		да = "Да";
	конецесли;
	Нажать ( да );

КонецПроцедуры
